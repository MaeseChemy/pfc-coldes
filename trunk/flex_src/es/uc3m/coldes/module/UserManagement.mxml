<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" width="100%" height="100%"
		   paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10"
		   creationComplete="init()">
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import es.uc3m.coldes.utils.popup.ConditionalPopUp;
			import es.uc3m.coldes.view.user.UserDataFormRegister;
			import es.uc3m.coldes.business.Constants;
			import es.uc3m.coldes.utils.popup.UtilPopUp;
			import mx.managers.PopUpManager;
			import es.uc3m.coldes.view.user.UserDataFormUpdate;
			import es.uc3m.coldes.business.UserService;
			import es.uc3m.coldes.entities.User;
			import mx.collections.ArrayCollection;
			
			[Bindable]
			private var users:ArrayCollection = new ArrayCollection();
					 
			private function init():void {
				new UserService().getAllUsers(callbackUsers);
			}

			private function callbackUsers(users:ArrayCollection):void {
				this.users=users;
			}

			/* Funcion de filtrado */
			private function userFilter(obj:Object):Boolean {
				var result:Boolean = false;
				var user:User = obj as User;
				if (searchText.text.length > 0 ) {
					if ((user.username != null && user.username.toUpperCase().indexOf(searchText.text.toUpperCase()) != -1) ||
						(user.name != null && user.name.toUpperCase().indexOf(searchText.text.toUpperCase()) != -1) ||
						(user.surname1 != null && user.surname1.toUpperCase().indexOf(searchText.text.toUpperCase()) != -1) ||
						(user.surname2 != null && user.surname2.toUpperCase().indexOf(searchText.text.toUpperCase()) != -1) ||
						(user.email != null && user.email.toUpperCase().indexOf(searchText.text.toUpperCase()) != -1)
						)
					{
						result = true;
					}
				}
				return result;
			}

			private function filter(event:Event=null):void {
				users.filterFunction = userFilter;
				users.refresh();
			}

			public function restart():void {
				init();
				searchText.text="";
			}
			
			public function edit(user:User):void{
				var userUpd:UserDataFormUpdate = new UserDataFormUpdate();
			
				PopUpManager.addPopUp(userUpd, this, true);
				PopUpManager.centerPopUp(userUpd);
				userUpd.management = true;
				userUpd.user = user;
				userUpd.callBackFunction = submitUpdate;
			}
			
			public function submitUpdate(user:User, passChange:Boolean):void{
				(new UserService()).updateUser(user, passChange, updateUserCallback);
			}
			
			private function updateUserCallback(updateResult:Boolean):void{
				if(updateResult){
					UtilPopUp.showMessagePopUP(resourceManager.getString('myResources','MESSAGETITLE.UPDATE'),
										       resourceManager.getString('myResources','MESSAGE.UPDATE'));
					refresh();
				}else{
					UtilPopUp.showMessagePopUP(resourceManager.getString('myResources','ERRORTITLE.SERVER'),
										       resourceManager.getString('myResources','ERROR.SERVER'));
				}
			}
			
			private function refresh():void{
				new UserService().getAllUsers(callbackUsers);
				searchText.text = "";
			}
			
			/*************************************/
			/**       REGISTER FUNCTIONS        **/
			/*************************************/
			private function doRegister():void {
				var userReg:UserDataFormRegister = new UserDataFormRegister();
				
				PopUpManager.addPopUp(userReg, this, true);
				PopUpManager.centerPopUp(userReg);
				userReg.callBackFunction = submitUserRegister;
			}
			
			public function submitUserRegister(user:User):void{
				(new UserService()).addUser(user, userRegisterCallback);
			}
			
			private function userRegisterCallback(newUser:String):void{
				if(newUser == ""){
					//User exist
					UtilPopUp.showMessagePopUP(resourceManager.getString('myResources','ERRORTITLE.REGISTER'),
										       resourceManager.getString('myResources','ERROR.REGISTER'));
				}else if (newUser == null){
					//Internal error
					UtilPopUp.showMessagePopUP(resourceManager.getString('myResources','ERRORTITLE.SERVER'),
										       resourceManager.getString('myResources','ERROR.SERVER'));
				}else{
					UtilPopUp.showMessagePopUP(resourceManager.getString('myResources','MESSAGETITLE.REGISTER'),
										       resourceManager.getString('myResources','MESSAGE.REGISTER'));
					refresh();
				}
			}
			
			
			private var popUpSalirConditional:ConditionalPopUp;
			private var userRemove:User;
			public function deleteUser(user:User):void {
				popUpSalirConditional = UtilPopUp.showConditionalPopUP(resourceManager.getString('myResources','MESSAGETITLE.REMOVEUSER'),
				resourceManager.getString('myResources','MESSAGE.REMOVEUSER'));
				userRemove = user;
				popUpSalirConditional.addEventListener(FlexEvent.REMOVE, doDeleteUser);
			}
			
			private function doDeleteUser(event:Event):void{
				if(popUpSalirConditional.answer){
					(new UserService()).deleteUser(userRemove,doDeleteUserCallback);
				}else{
					userRemove = null;
				}
			}

			private function doDeleteUserCallback(result:Boolean):void {
				refresh();
			}
			
			
		]]>
	</mx:Script>
	<mx:Canvas width="100%" height="100%">
	<mx:VBox id="vbox" width="100%" height="100%" >
		<mx:HBox width="100%" verticalAlign="middle">
			<mx:FormItem label="{resourceManager.getString('myResources','USERMANAGEMENT.SEARCH')}:">
				<mx:TextInput id="searchText" enter="filter()"/>
			</mx:FormItem>
			<mx:Button label="{resourceManager.getString('myResources','USERMANAGEMENT.SEARCHBTN')}" click="filter()"/>
			<mx:Button click="refresh()" toolTip="{resourceManager.getString('myResources','USERMANAGEMENT.TOOLREFRESH')}">
				<mx:icon>@Embed(source='../../../../assets/images/refresh.png')</mx:icon>
			</mx:Button>
		</mx:HBox>
		<mx:Button click="doRegister()" label="{resourceManager.getString('myResources','USERMANAGEMENT.TOOLADDUSER')}"
				styleName="HomeButtonSec" height="30">
			<mx:icon>@Embed(source='../../../../assets/images/user_add.png')</mx:icon>
		</mx:Button>
		<mx:DataGrid id="gridUserMan" width="100%" height="100%" dataProvider="{users}" verticalScrollPolicy="auto" selectable="false">
			<mx:columns>
				<mx:DataGridColumn width="70" resizable="false" itemRenderer="es.uc3m.coldes.view.user.UserManagementAcctions"/>
				<mx:DataGridColumn headerText="{resourceManager.getString('myResources','USERMANAGEMENT.USER')}">
					<mx:itemRenderer>
						<mx:Component >
							<mx:Text text="{(data).username}" 
									color="{(data).active == Constants.DEACTIVE?0xff0000:0x008000}">
								<mx:Script>
									<![CDATA[
										import es.uc3m.coldes.business.Constants;
									]]>
								</mx:Script>
							</mx:Text>
						</mx:Component>
					</mx:itemRenderer>
				</mx:DataGridColumn>
				<mx:DataGridColumn headerText="{resourceManager.getString('myResources','USERMANAGEMENT.NAME')}" dataField="name"/>
				<mx:DataGridColumn headerText="{resourceManager.getString('myResources','USERMANAGEMENT.SURNAME1')}" dataField="surname1"/>
				<mx:DataGridColumn headerText="{resourceManager.getString('myResources','USERMANAGEMENT.SURNAME2')}" dataField="surname2"/>
			</mx:columns>
		</mx:DataGrid>
	</mx:VBox>
	</mx:Canvas>
</mx:Module>
